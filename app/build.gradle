apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

ext.ver_major = 1
ext.ver_minor = 0
ext.ver_patch = 4
ext.min_sdk   = 19

private Integer gen_version_code()
{
    // version code is <MIN_SDK>0<2-digit MAJOR><2-digit MINOR><2-digit PATCH>
    return ext.min_sdk   * 10000000 +
           ext.ver_major *    10000 +
           ext.ver_minor *      100 +
           ext.ver_patch
}

private String gen_version_name()
{
    return "${ext.ver_major}.${ext.ver_minor}.${ext.ver_patch}"
}

android {
    compileSdkVersion 28
    dataBinding.enabled = true
    defaultConfig {
        applicationId "org.mattvchandler.a2050"
        minSdkVersion project.ext.min_sdk
        targetSdkVersion 28
        versionCode gen_version_code()
        versionName gen_version_name()
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
        externalNativeBuild {
            cmake {
                arguments "-DANDROID_STL=c++_static"
                cppFlags "-std=c++1z -frtti -fexceptions -Wall"
            }
        }
    }
    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    externalNativeBuild {
        cmake {
            path "CMakeLists.txt"
        }
    }
    compileOptions {
        targetCompatibility 1.8
        sourceCompatibility 1.8
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'androidx.appcompat:appcompat:1.1.0-alpha04'
    implementation 'androidx.preference:preference:1.1.0-alpha04'
    implementation 'com.google.android.material:material:1.1.0-alpha05'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
}
repositories {
    mavenCentral()
}
